apply plugin: "jacoco"
apply plugin: 'com.github.kt3k.coveralls'

def coverageSourceDirs = [
        'App/src/main/java',
        'Core/src/main/java']


def applyDaggerWorkaround(String pathWithDaggerClasses) {
    def filePath = new File(pathWithDaggerClasses)
    if (filePath.exists()) {
        filePath.eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

def revertDaggerWorkaround(String pathWithDaggerClasses) {
    def filePath = new File(pathWithDaggerClasses)
    if (filePath.exists()) {
        filePath.eachFileRecurse { file ->
            if (file.name.contains('$ModuleAdapter')) {
                file.renameTo(file.path.replace('$ModuleAdapter', '$$ModuleAdapter'))
            }
            if (file.name.contains('$InjectAdapter')) {
                file.renameTo(file.path.replace('$InjectAdapter', '$$InjectAdapter'))
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    // exclude generated classes from code coverage report because not all generated methods will be used
    def defaultExcludes = ['**/R.class', '**/R$*.class', '**/*_*', '**/BuildConfig.class', '**/*InjectAdapter.class', '**/*ModuleAdapter*.class']

    classDirectories = fileTree(dir: 'App/build/intermediates/classes/debug', excludes: defaultExcludes)
    classDirectories += fileTree(dir: 'Core/build/intermediates/classes/debug', exclude: defaultExcludes)

    additionalSourceDirs = files(coverageSourceDirs)
    executionData = files(
            'App/build/outputs/code-coverage/connected/coverage.ec',
            'App/build/jacoco/testDebug.exec',
            'AppCT/build/jacoco/testDebug.exec',
            'Core/build/jacoco/testDebug.exec')

    // fix issue with double $$ at generate report task
    doFirst {
        applyDaggerWorkaround('App/build/intermediates/classes/')
        applyDaggerWorkaround('Core/build/intermediates/classes/')
    }
    // avoid some side effects through revert renaming
    doLast {
        revertDaggerWorkaround('App/build/intermediates/classes/')
        revertDaggerWorkaround('Core/build/intermediates/classes/')
    }
}

def isTravis = "true".equals(System.getenv("TRAVIS"))

// Send code coverage report to coveralls with coveralls-gradle-plugin. To get the plugin working
// we need to include the coverageSourceDirs as main sources. But this conflicts with android
// studio and also with jacocoTestReport task.
compileJava.enabled = false
coveralls {
    if(isTravis) { // avoid android studio source root conflicts
        sourceSets.main.java.srcDirs += coverageSourceDirs
    }
}