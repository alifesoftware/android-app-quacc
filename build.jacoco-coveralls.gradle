apply plugin: "jacoco"
apply plugin: 'com.github.kt3k.coveralls'

jacoco {
    toolVersion = "0.7.4.201502262128"
}

def isTravis = "true".equals(System.getenv("TRAVIS"))

task configureCoverageReport {
    doFirst {
        def includeAppSrc = false;
        def includeCoreSrc = false;
        def includeDatabaseSrc = false;
        def coverageFiles = []
        def coverageSourceDirs = []

        // exclude generated classes from code coverage report because not all generated methods will be used
        def defaultExcludes = ['**/R.class', '**/R$*.class', '**/BuildConfig.class' // generated by android
                               , '**/*_*' // generated by androidannotaions
                               , '**/*InjectAdapter.class', '**/*ModuleAdapter*.class' // generated by dagger
                               , '**/database/provider/*' // generated database handling
                               , '**/apptest/*']
        // just ignore our dummy activity for instrumentation tests

        // Don't take not existing coverage report files or the report will be empty
        def currentCoverageFile = 'App/build/outputs/code-coverage/connected/coverage.ec'
        if (file(currentCoverageFile).exists()) {
            includeAppSrc = true;
            includeCoreSrc = true;
            includeDatabaseSrc = true;
            coverageFiles += currentCoverageFile
        }
        currentCoverageFile = 'App/build/jacoco/testDebug.exec'
        if (file(currentCoverageFile).exists()) {
            includeAppSrc = true;
            coverageFiles += currentCoverageFile
        }
        currentCoverageFile = 'AppCT/build/jacoco/testDebug.exec'
        if (file(currentCoverageFile).exists()) {
            includeAppSrc = true;
            includeCoreSrc = true;
            includeDatabaseSrc = true;
            coverageFiles += currentCoverageFile
        }
        currentCoverageFile = 'Database/jacoco.exec'
        if (file(currentCoverageFile).exists()) {
            includeDatabaseSrc = true;
            coverageFiles += currentCoverageFile
        }
        currentCoverageFile = 'Core/build/jacoco/testDebug.exec'
        if (file(currentCoverageFile).exists()) {
            includeCoreSrc = true;
            coverageFiles += currentCoverageFile
        }

        // Don't include classes from modules we haven't executed
        if (includeAppSrc) {
            coverageSourceDirs += 'App/src/main/java'
            tasks.jacocoTestReport.classDirectories += fileTree(dir: 'App/build/intermediates/classes/debug', excludes: defaultExcludes)
        }
        if (includeCoreSrc) {
            coverageSourceDirs += 'Core/src/main/java'
            tasks.jacocoTestReport.classDirectories += fileTree(dir: 'Core/build/intermediates/classes/debug', exclude: defaultExcludes)
        }
        if (includeDatabaseSrc) {
            coverageSourceDirs += 'Database/src/gen/java'
            coverageSourceDirs += 'Database/src/main/java'
            tasks.jacocoTestReport.classDirectories += fileTree(dir: 'Database/build/intermediates/classes/debug', exclude: defaultExcludes)
        }

        tasks.jacocoTestReport.additionalSourceDirs = files(coverageSourceDirs)
        tasks.jacocoTestReport.executionData = files(coverageFiles)

        // Send code coverage report to coveralls with coveralls-gradle-plugin. To get the plugin working
        // we need to include the coverageSourceDirs as main sources. But this conflicts with android
        // studio and also with jacocoTestReport task.
        if (isTravis) { // avoid android studio source root conflicts
            tasks.coveralls {
                sourceSets.main.java.srcDirs += coverageSourceDirs
            }
        }
    }
}

compileJava.enabled = false

tasks.jacocoTestReport.dependsOn tasks.configureCoverageReport
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// Send code coverage report to coveralls with coveralls-gradle-plugin. To get the plugin working
// we need to include the coverageSourceDirs as main sources. But this conflicts with android
// studio and also with jacocoTestReport task.
compileJava.enabled = false
coveralls {
    if (isTravis) { // avoid android studio source root conflicts
        sourceSets.main.java.srcDirs += ['App/src/main/java', 'Core/src/main/java']
    }
}