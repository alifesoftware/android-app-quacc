buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // support code generation (android annotaions, dagger, ...)
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

plugins {
    id "de.undercouch.download" version "1.2"
}

apply plugin: 'com.android.library'

def dbSchemaPath = "src/main/json/schema"
def dbClassesPath = "src/gen/java"

apply from: "../build.jacoco-already-instrumented-workaround.gradle"

android {
    compileSdkVersion 21
    buildToolsVersion "23.0.2"
    publishNonDefault true

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    sourceSets.main.java.srcDirs += dbClassesPath
    sourceSets.main.java.srcDirs += "src/main/json"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {

        }
    }

    lintOptions {
        // robolectric does only support sdk v21 current
        disable 'OldTargetApi'
    }
}


apply from: "build.apt.gradle"
apply from: "../build.androidannotations.gradle"
apply from: "../build.robolectric.gradle"
apply from: "../build.test-artifact.gradle"

// add compile main sources for test runs https://github.com/nenick/AndroidStudioAndRobolectric/issues/7
afterEvaluate { project ->
    android.libraryVariants.each { variant ->
        tasks.getByName("test${variant.name.capitalize()}") {
            dependsOn "assemble${variant.name.capitalize()}"
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:21.0.3'
    compile 'com.google.guava:guava:18.0'
    testCompile 'org.javassist:javassist:3.20.0-GA'
}

task generateDatabaseFiles << {
    if (!file("$buildDir/android_contentprovider_generator-1.9.2-bundle.jar").exists()) {
        download {
            src 'https://github.com/BoD/android-contentprovider-generator/releases/download/v1.9.2/android_contentprovider_generator-1.9.2-bundle.jar'
            dest buildDir
        }
    }
    file("src/gen").deleteDir()
    exec {
        executable "java"
        args = ["-jar", "$buildDir/android_contentprovider_generator-1.9.2-bundle.jar", "-i", dbSchemaPath, "-o", dbClassesPath]
    }
}

afterEvaluate {
    tasks.testDebug {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
        jvmArgs "-XX:MaxPermSize=512m"
    }
}