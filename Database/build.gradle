buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // support code generation (android annotaions, dagger, ...)
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

plugins {
    id "de.undercouch.download" version "1.2"
}

apply plugin: 'com.android.library'

def dbSchemaPath = "src/main/json/schema"
def dbClassesPath = "src/gen/java"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"
    publishNonDefault true

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    sourceSets.main.java.srcDirs += dbClassesPath
    sourceSets.main.java.srcDirs += "src/main/json"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }

    lintOptions {
        // robolectric does only support sdk v21 current
        disable 'OldTargetApi'
    }
}


apply from: "build.apt.gradle"
apply from: "../build.androidannotations.gradle"
apply from: "../build.dagger.gradle"
apply from: "../build.robolectric.gradle"

// add compile main sources for test runs https://github.com/nenick/AndroidStudioAndRobolectric/issues/7
afterEvaluate { project ->
    android.libraryVariants.each { variant ->
        tasks.getByName("test${ variant.name.capitalize()}") {
            dependsOn "assemble${ variant.name.capitalize()}"
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:21.0.3'
    compile 'com.getbase.android.forger:forger:0.1'
    //testCompile 'org.chalup.microorm:microorm:0.5'
    //testCompile 'org.chalup.thneed:thneed:0.7'
}

task generateDatabaseFiles << {
    if(!file("$buildDir/android_contentprovider_generator-1.9.2-bundle.jar").exists()) {
        download {
            src 'https://github.com/BoD/android-contentprovider-generator/releases/download/v1.9.2/android_contentprovider_generator-1.9.2-bundle.jar'
            dest buildDir
        }
    }

    file("src/gen").deleteDir()

    exec {
        executable "java"
        args "-jar", "$buildDir/android_contentprovider_generator-1.9.2-bundle.jar", "-i", dbSchemaPath, "-o", dbClassesPath
    }
}