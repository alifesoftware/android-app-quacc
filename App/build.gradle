buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // support code generation (android annotaions, dagger, ...)
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}
def isTravis = "true".equals(System.getenv("TRAVIS"))

apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "de.nenick.quacc"
        minSdkVersion 17
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnit4"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-dev"
            testCoverageEnabled = true
            buildConfigField "String", "MODULE_PATH", "\"$projectDir\""
        }
    }

    dexOptions {
        // speed up build on Travis CI
        // since adding coverage to core module this produce java.lang.IllegalArgumentException: already added: Lcom/vladium/emma/rt/RT;
        // preDexLibraries = !isTravis
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

apply from: "build.apt.gradle"
apply from: "../build.androidannotations.gradle"
apply from: "../build.dagger.gradle"
apply from: "../build.robolectric.gradle"

dependencies {
    releaseCompile project(path: ':Core', configuration: 'release')
    debugCompile project(path: ':Core', configuration: 'debug')

    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.melnykov:floatingactionbutton:1.2.0'

    testCompile project(':RobolectricPages')

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'

}

task myTestsJar(type: Jar) {
    //classifier = 'tests'
    from "src/test/java"
}

configurations {
    testArtifacts
}

artifacts {
    testArtifacts myTestsJar
}